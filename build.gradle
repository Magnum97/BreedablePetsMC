// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

// We use this to replace version in plugin.yml
import org.apache.tools.ant.filters.ReplaceTokens


plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group= 'me.magnum' // Put your groupId here
version= '1.1.0' // Put your version here
archivesBaseName = 'BreedablePets' // Put your artifactId here
mainClassName = 'me.magnum.breedablepets.Main'
targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }

    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" } // Spigot Repo
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // Sonatype snapshots
    maven { url 'https://jitpack.io' }
}
compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    implementation 'fr.mrmicky:FastParticles:1.2.0'

    implementation "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    implementation 'me.magnum.lib:CommonLibrary:1.0.3-SNAPSHOT'
//    implementation 'com.github.kangarko:Remain:3.2.0'
}
task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "shadow" // Default value is "shadow"

}
/* This will replace '@VERSION@' in the plugin.yml with the version declared above, just put '@VERSION@' for the version in the plugin.yml */
processResources {
    filter ReplaceTokens, tokens: [
            "VERSION": version,
            "NAME"   : archivesBaseName,
            "MAIN"   : mainClassName,
    ]
}
shadowJar {
    archiveClassifier.set("dev")
    relocate 'co.aikar.commands', 'me.magnum.breedablepets.acf'
}

build.dependsOn shadowJar

compileJava.options.encoding = 'UTF-8' // Encode your source code with UTF-8
